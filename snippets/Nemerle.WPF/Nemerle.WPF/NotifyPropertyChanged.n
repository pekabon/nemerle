using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;
using System.ComponentModel;

namespace Nemerle.WPF
{
    using NotifyPropertyChangedHelper;

    [MacroUsage(MacroPhase.BeforeInheritance, MacroTargets.Class, Inherited = false, AllowMultiple = false)]  
    public macro NotifyPropertyChanged(tb : TypeBuilder, raiseChangedMethod = null)
    {
        ignore(raiseChangedMethod);

        AddInterface(tb);
    }

    [MacroUsage(MacroPhase.BeforeTypedMembers, MacroTargets.Class, Inherited = false, AllowMultiple = false)]  
    public macro NotifyPropertyChanged(tb : TypeBuilder, raiseChangedMethod = <[ () ]>)
    {
        ImplementInterface(Macros.ImplicitCTX(), tb, raiseChangedMethod, GetUndecoratedName());
    }

    [MacroUsage(MacroPhase.WithTypedMembers, MacroTargets.Class, Inherited = false, AllowMultiple = false)]  
    public macro NotifyPropertyChanged(tb : TypeBuilder, raiseChangedMethod =  <[ () ]>)
    {
        ignore(raiseChangedMethod);

        FixProperties(tb, GetUndecoratedName());
    }

    public macro RaisePropertyChanged(property : PExpr, raiseChangedMethod = <[ () ]>)
    {
        RaisePropertyChanged(Macros.ImplicitCTX().CurrentTypeBuilder,
                             property,
                             raiseChangedMethod,
                             GetUndecoratedName());
    }

    [MacroUsage(MacroPhase.BeforeInheritance, MacroTargets.Property, Inherited = false, AllowMultiple = false)]  
    public macro NotifyChangedOptions(tb : TypeBuilder, property : ParsedProperty, params options : list[PExpr])
    {
        foreach(option in options) 
        {
            | <[ IgnoreSetter ]> => RegisterIgnored(tb, property);
            | e => Message.Error(e.Location, 
                                 $"Unsupported argument '$e' in $(GetUndecoratedName()) macro, valid options are: 'Dependent = [Prop1, Prop2]' and 'IgnoreSetter'.")
        }
    }

    module NotifyPropertyChangedHelper
    {       
        public AddInterface(tb : TypeBuilder) : void
        {
            tb.AddImplementedInterface(<[ System.ComponentModel.INotifyPropertyChanged ]>);
        }

        public ImplementInterface(typer            : Typer, 
                                tb                 : TypeBuilder, 
                                raiseChangedMethod : PExpr, 
                                macroName          : string) : void
        {
            Macros.DefineCTX(typer);

            when (tb.IsDelegate  || 
                  tb.IsEnum      || 
                  tb.IsInterface || 
                  tb.IsModule)
                Message.Error(tb.Location, 
                              $"Macro $macroName is not valid on this declaration type. It is only valid on 'class' declarations.");

            def interfaceAlreadyImplemented = tb.BaseClass.TryRequire(<[ ttype: System.ComponentModel.INotifyPropertyChanged ]>);

            def raiseChangedMethodName = match(raiseChangedMethod)
                                         {
                                             | <[ $(methodName : name) ]> => methodName
                                             | <[()]> => Name("OnPropertyChanged")
                                             | _ =>  Message.Error(tb.Location, 
                                                                   $"Specify a valid method for property changed event raising (e.g. $macroName(PropName, OnPropertyChanged)).");
                                                     Name("OnPropertyChanged")
                                         };

            unless (interfaceAlreadyImplemented)
            {
                tb.Define(<[ decl: public event PropertyChanged : System.ComponentModel.PropertyChangedEventHandler; ]>);

                def raiseMethodDecl = <[ decl:
                                         protected virtual $(raiseChangedMethodName : name)(propertyName: string) : void
                                         {
                                             def handler = PropertyChanged;
                                             when (handler != null)
                                                 handler(this, System.ComponentModel.PropertyChangedEventArgs(propertyName));
                                         }
                                        ]>;

                tb.Define(raiseMethodDecl);
            }

            tb.UserData[_userDataKey] = raiseChangedMethodName;
        }

        public RaisePropertyChanged(tb : TypeBuilder, property : PExpr, raiseChangedMethod : PExpr, macroName : string) : PExpr
        {
            def OnError(message)
            {
                Message.Error(raiseChangedMethod.Location, message);
                <[]>
            }
                
            def CreateRaiseMethod(propName, raiseChangedMethodName : option[Name])
            {
                match(tb.GetProperties().Filter(prop => prop.Name == propName))
                {
                   | [_] => raiseChangedMethodName.Map(methodName => <[ $(methodName : name)($(propName : string)); ]>) 
                            ?? OnError($"Specify a valid method for property changed event raising (e.g. $macroName(PropName, OnPropertyChanged)).")
                   | []  => OnError($"Class '$(tb.FullName)' does not contain a property with the name '$propName'.")
                }
            }

            def raiseChangedMethodName = match(raiseChangedMethod)
                             {
                                 | <[ $(methodName : name) ]> with name = methodName
                                 | <[()]> when tb.UserData.Contains(_userDataKey) with name = tb.UserData[_userDataKey] :> Name
                                      => if (tb.MethodExists(name)) Some(name) else None()
                                 | _  => None()
                             };

            match(property)
            {
                | propExpr is PExpr.Ref => CreateRaiseMethod(propExpr.name.Id, raiseChangedMethodName)
                | _ => OnError($"Specify a property for changed event raising (e.g. $macroName(PropName)).")
            }
        }

        public FixProperties(tb : TypeBuilder, macroName : string) : void
        {
            def raiseChangedMethodName = tb.UserData[_userDataKey] :> Name;

            if(!tb.MethodExists(raiseChangedMethodName))
            {
                Message.Error(tb.Location, 
                              $"Specify a valid method for property changed event raising (e.g. [$macroName(OnPropertyChanged)]).");
            }
            else
            {
                def properties = tb.GetProperties(BindingFlags.DeclaredOnly | 
                                                  BindingFlags.Instance     | 
                                                  BindingFlags.Public       | 
                                                  BindingFlags.NonPublic).
                                    Filter(property => !_ignoredProperties.Contains(tb, property.Name));

                foreach(propertyBuilder is PropertyBuilder in properties)
                {
                    match(propertyBuilder.GetSetter())
                    {
                        | setter is MethodBuilder => 
                            def loc = setter.Body.Location;
                            setter.Body = <[ $(setter.Body);
                                            $(raiseChangedMethodName : name)($(propertyBuilder.Name : string)); ]>;
                            setter.Body.Location = loc;
                        | _ => ()
                    }
                }
            }
        }

        public RegisterIgnored(tb : TypeBuilder, property : ClassMember.Property) : void
        {
            _ = _ignoredProperties.Add(tb, property.Name);
        }
        
        public GetUndecoratedName(this macroInstance : IMacro) : string
        {
            macroInstance.GetName().Split(':').First()
        }

        private MethodExists(this tb : TypeBuilder, methodName : Name) : bool
        {
            tb.GetMethods(BindingFlags.Instance | BindingFlags.Public | BindingFlags.NonPublic).
               Exists(method => method.Name == methodName.Id)
        }

        private _ignoredProperties = SCG.HashSet.[TypeBuilder * string]();
        private _userDataKey = Guid.NewGuid();
    }
}